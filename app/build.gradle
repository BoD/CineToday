apply plugin: 'com.android.application'
if (buildProperties.enableFabric.toBoolean()) apply plugin: 'io.fabric'
apply plugin: 'org.jraf.acpg.gradleplugin'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        applicationId 'org.jraf.android.cinetoday'
        minSdkVersion 25
        targetSdkVersion versions.targetSdk
        versionCode buildNumber
        versionName buildProperties.versionName

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'

        buildConfigField 'String', 'GIT_SHA1', "\"${gitSha1}\""
        buildConfigField 'String', 'BUILD_DATE', "\"${buildDate}\""
        // For now we enable strict mode for all the configs
        buildConfigField 'boolean', 'STRICT_MODE', 'true'

        resConfigs 'en', 'fr'

        // Useful for api keys in the manifest (Maps, Crashlytics, ...)
        manifestPlaceholders = buildProperties

        // Setting this to false disables the png generation at buildtime
        // (see http://android-developers.blogspot.fr/2016/02/android-support-library-232.html)
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        release {
            storeFile file(buildProperties.signingStoreFile)
            storePassword buildProperties.signingStorePassword
            keyAlias buildProperties.signingKeyAlias
            keyPassword buildProperties.signingKeyPassword
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            buildConfigField 'boolean', 'CRASH_REPORT', 'false'
        }

        release {
            buildConfigField 'boolean', 'CRASH_REPORT', 'true'

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            shrinkResources true
            signingConfig signingConfigs.release
        }
    }

    dataBinding {
        enabled = true
    }

    lintOptions {
        abortOnError true
        textReport true
        ignoreWarnings true
    }
}

android.applicationVariants.all { variant ->
    // Create new copy tasks, for release builds
    if (variant.buildType.name == 'release') {
        variant.outputs.each { output ->
            def apkName = "${globalProjectName}-${project.name}-${android.defaultConfig.versionCode}-${variant.flavorName}-signed.apk"

            // Copy the apk to the 'etc' folder
            def copyApkToEtc = tasks.create("copy${variant.name.capitalize()}ApkToEtc", Copy)
            copyApkToEtc.from(output.outputFile)
            copyApkToEtc.into('../etc/apk')
            copyApkToEtc.rename output.outputFile.name, apkName

            // Copy the apk to the deploy folder
            def copyApkToDeploy = tasks.create("copy${variant.name.capitalize()}ApkToDeploy", Copy)
            copyApkToDeploy.from(output.outputFile)
            copyApkToDeploy.into(buildProperties.deployFolder)
            copyApkToDeploy.rename output.outputFile.name, apkName

            // Make the copy tasks run after the assemble tasks of the variant
            variant.assemble.finalizedBy(copyApkToEtc, copyApkToDeploy)
        }
    }
}

acpg {
    providerJavaPackage 'org.jraf.android.cinetoday.provider'
    authority 'org.jraf.android.cinetoday.provider'
    providerClassName 'CineTodayProvider'
    databaseFileName 'cinetoday.db'
    databaseVersion 1
    sqliteOpenHelperCallbacksClassName 'CineTodaySQLiteOpenHelperCallbacks'
    enableForeignKeys true
    useAnnotations true
    useSupportLibrary false
}

dependencies {
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.2.2'
    testCompile 'org.easytesting:fest-assert-core:2.0M10'

    // Support library
    compile "com.android.support:support-fragment:$versions.supportLibrary"
    compile "com.android.support:support-annotations:$versions.supportLibrary"
    compile "com.android.support:percent:$versions.supportLibrary"
    compile "com.android.support:palette-v7:$versions.supportLibrary"

    //  Wear
    provided 'com.google.android.wearable:wearable:2.0.0'
    compile 'com.google.android.support:wearable:2.0.0'

    // Play Services
    compile "com.google.android.gms:play-services-gcm:$versions.playServices"

    // Fabric / Crashlytics
    compile('com.crashlytics.sdk.android:crashlytics:2.6.6@aar') { transitive = true; }

    // JRAF
    annotationProcessor 'org.jraf:prefs-compiler:1.1.1'
    compile 'org.jraf:prefs:1.1.1'
    compile 'com.github.BoD:jraf-android-util:-SNAPSHOT'
//    compile 'org.jraf:jraf-android-util:1.0.0'

    // OkHttp
    compile 'com.squareup.okhttp3:okhttp:3.6.0'

    // Glide
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.github.bumptech.glide:okhttp3-integration:1.4.0@jar'

    // Dagger
    compile "com.google.dagger:dagger:$versions.dagger"
    annotationProcessor "com.google.dagger:dagger-compiler:$versions.dagger"
    provided "javax.annotation:jsr250-api:1.0"
}
