apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
if (buildProperties.enableFabric.toBoolean()) apply plugin: 'io.fabric'
apply plugin: 'com.getkeepsafe.dexcount'
// Commented for now because of https://issuetracker.google.com/issues/79122163
//apply plugin: 'com.google.android.gms.strict-version-matcher-plugin'


android {
    compileSdkVersion versions.build.compileSdk

    defaultConfig {
        applicationId 'org.jraf.android.cinetoday'
        minSdkVersion versions.build.minSdk
        targetSdkVersion versions.build.targetSdk
        versionCode buildNumber
        versionName buildProperties.versionName

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'

        // For now we enable strict mode for all the configs
        buildConfigField 'boolean', 'STRICT_MODE', 'true'
        // For now we enable debug logs all the configs
        buildConfigField 'boolean', 'DEBUG_LOGS', 'true'

        resConfigs 'en', 'fr'

        // Useful for api keys in the manifest (Maps, Crashlytics, ...)
        manifestPlaceholders = buildProperties

        // Setting this to true disables the png generation at buildtime
        // (see http://android-developers.blogspot.fr/2016/02/android-support-library-232.html)
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        release {
            storeFile file(buildProperties.signingStoreFile)
            storePassword buildProperties.signingStorePassword
            keyAlias buildProperties.signingKeyAlias
            keyPassword buildProperties.signingKeyPassword
        }
    }

    buildTypes {
        debug {
            buildConfigField 'String', 'GIT_SHA1', "\"dev\""
            buildConfigField 'String', 'BUILD_DATE', "\"dev\""
            buildConfigField 'boolean', 'CRASH_REPORT', 'false'
        }

        release {
            buildConfigField 'String', 'GIT_SHA1', "\"${gitSha1}\""
            buildConfigField 'String', 'BUILD_DATE', "\"${buildDate}\""
            buildConfigField 'boolean', 'CRASH_REPORT', 'true'

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            shrinkResources true
            signingConfig signingConfigs.release
        }
    }

    dataBinding {
        enabled = true
    }

    lintOptions {
        abortOnError true
        textReport true
        ignoreWarnings true
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

android.applicationVariants.all { variant ->
    // Create new copy tasks, for release builds
    if (variant.buildType.name == 'release') {
        variant.outputs.each { output ->
            def outputFile = file("build/outputs/apk/${variant.flavorName}/release/$output.outputFileName")
            def apkName = "${globalProjectName}-${project.name}-${android.defaultConfig.versionCode}-${variant.flavorName}-signed.apk"

            // Copy the apk to the 'etc' folder
            def copyApkToEtc = tasks.create("copy${variant.name.capitalize()}ApkToEtc", Copy)
            copyApkToEtc.from(outputFile)
            copyApkToEtc.into('../etc/apk')
            copyApkToEtc.rename output.outputFileName, apkName

            // Copy the apk to the deploy folder
            def copyApkToDeploy = tasks.create("copy${variant.name.capitalize()}ApkToDeploy", Copy)
            copyApkToDeploy.from(outputFile)
            copyApkToDeploy.into(buildProperties.deployFolder)
            copyApkToDeploy.rename output.outputFileName, apkName

            // Make the copy tasks run after the assemble tasks of the variant
            variant.assemble.finalizedBy(copyApkToEtc, copyApkToDeploy)
        }
    }
}

dependencies {
    // Tests
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$versions.testing.espresso", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation "junit:junit:$versions.testing.junit"
    testImplementation "org.robolectric:robolectric:$versions.testing.robolectric"
    testImplementation "org.easytesting:fest-assert-core:$versions.testing.fest"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin"

    // Support library
    implementation "com.android.support:support-fragment:$versions.supportLibrary"
    implementation "com.android.support:support-annotations:$versions.supportLibrary"
    implementation "com.android.support:percent:$versions.supportLibrary"
    implementation "com.android.support:palette-v7:$versions.supportLibrary"
    implementation "com.android.support:preference-v14:$versions.supportLibrary"

    //  Wear
    implementation "com.google.android.support:wearable:$versions.androidWearable"
    implementation "com.google.android.wearable:wearable:$versions.androidWearable"
    implementation "com.android.support:wear:$versions.supportLibrary"

    // Play Services
    implementation "com.google.android.gms:play-services-gcm:$versions.playServices"

    // Fabric / Crashlytics
    implementation("com.crashlytics.sdk.android:crashlytics:$versions.crashlytics@aar") { transitive = true }

    // JRAF
    kapt "org.jraf:prefs-compiler:$versions.prefs"
    implementation "org.jraf:prefs:$versions.prefs"
    implementation 'com.github.BoD:jraf-android-util:-SNAPSHOT'
//    implementation 'org.jraf:jraf-android-util:1.0.0'

    // OkHttp
    implementation "com.squareup.okhttp3:okhttp:$versions.okHttp"
    implementation "com.squareup.okhttp3:logging-interceptor:$versions.okHttp"

    // Glide
    implementation "com.github.bumptech.glide:glide:$versions.glide"
    kapt "com.github.bumptech.glide:compiler:$versions.glide"
    implementation "com.github.bumptech.glide:okhttp3-integration:$versions.glide"

    // Dagger
    implementation "com.google.dagger:dagger:$versions.dagger"
    kapt "com.google.dagger:dagger-compiler:$versions.dagger"
    compileOnly "javax.annotation:jsr250-api:$versions.jsr250"

    // Architecture components
    implementation "android.arch.lifecycle:extensions:$versions.archLifecycle"
    implementation "android.arch.lifecycle:common-java8:$versions.archLifecycle"

    // Room
    implementation "android.arch.persistence.room:runtime:$versions.archRoom"
    kapt "android.arch.persistence.room:compiler:$versions.archRoom"

    // Rx
    implementation "io.reactivex.rxjava2:rxandroid:$versions.rxAndroid"
    implementation "io.reactivex.rxjava2:rxjava:$versions.rxJava"
}

// Necessary for Prefs to work (see https://kotlinlang.org/docs/reference/kapt.html)
kapt {
    correctErrorTypes = true
}
