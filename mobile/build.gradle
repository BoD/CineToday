buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    defaultConfig {
        applicationId 'org.jraf.android.moviestoday'
        minSdkVersion 18
        targetSdkVersion 23
        versionCode buildNumber
        versionName buildProperties['versionName']

        buildConfigField 'String', 'GIT_SHA1', '"' + gitSha1() + '"'
        buildConfigField 'String', 'BUILD_DATE', '"' + buildDate() + '"'
        // For now we enable strict mode for all the configs
        buildConfigField 'boolean', 'STRICT_MODE', 'true'

        resConfigs 'en', 'fr'
    }

    signingConfigs {
        release {
            storeFile file(buildProperties['key-store'])
            storePassword buildProperties['key-store-password']
            keyAlias buildProperties['key-alias']
            keyPassword buildProperties['key-alias-password']
        }
    }

    buildTypes {
        debug {
            buildConfigField 'boolean', 'CRASH_REPORT', 'false'

            minifyEnabled false
        }

        release {
            buildConfigField 'boolean', 'CRASH_REPORT', 'true'

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            shrinkResources true
            signingConfig signingConfigs.release
        }
    }
}

def releaseTask = tasks.create('release')
releaseTask << {
    // Increment build number
    buildNumberProperties['build.number'] = (buildNumber + 1).toString()
    buildNumberProperties.store(buildNumberFile.newWriter(), null)
}

android.applicationVariants.all { variant ->
    if (variant.buildType.name == 'release') {
        variant.outputs.each { output ->
            def apkName = "${globalProjectName}-${android.defaultConfig.versionCode}-${variant.flavorName}-signed.apk"
            // Copy the apk to the 'etc' folder
            def copyApkTask = tasks.create("copy${variant.name.capitalize()}ApkToEtc", Copy)
            copyApkTask.from(output.outputFile)
            copyApkTask.into('../etc/apk')
            copyApkTask.rename output.outputFile.name, apkName
            copyApkTask.dependsOn variant.assemble

            releaseTask.dependsOn copyApkTask

            // Copy the apk to the deploy folder
            copyApkTask = tasks.create("copy${variant.name.capitalize()}ApkToDeploy", Copy)
            copyApkTask.from(output.outputFile)
            copyApkTask.into(buildProperties['deploy-folder'])
            copyApkTask.rename output.outputFile.name, apkName
            copyApkTask.dependsOn variant.assemble

            releaseTask.dependsOn copyApkTask
        }

        // Copy the proguard mapping to the 'etc' folder
        def copyMappingTask = tasks.create("copy${variant.name.capitalize()}ProguardMappingToEtc", Copy)
        copyMappingTask.from(variant.mappingFile)
        copyMappingTask.into('../etc/proguard')
        copyMappingTask.rename variant.mappingFile.name, "mapping-${android.defaultConfig.versionCode}-${variant.flavorName}.txt"
        copyMappingTask.dependsOn variant.assemble

        releaseTask.dependsOn copyMappingTask
    }
}

dependencies {
    compile project(':common')
    wearApp project(':wear')
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.android.support:preference-v7:23.1.1'
    compile 'com.android.support:preference-v14:23.1.1'
    compile 'com.squareup.okhttp:okhttp:2.7.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.google.android.gms:play-services-gcm:8.4.0'
    apt 'org.jraf:prefs-compiler:1.0.1'
    compile 'org.jraf:prefs:1.0.1'
}
